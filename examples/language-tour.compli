// Every compli program needs a `main` function.
// It takes no arguments and returns an int that gets printed when running the program.
def main(): int = 42

// There are three main data types: int, float and bool.
def return_float(): float = 0.5
def return_bool(): bool = true

// A function consists of a list of parameters, a return type and a body expression.
def foo(a: int, c: bool): float = 12.34

// Many common basic operations are supported.
def add(a: int, b: int): int = a + b
def sub(a: float, b: float): float = a - b
def gauss(n: int): int = (n * (n + 1)) / 2
def boolean(a: bool, b: bool, c: bool): bool = !a && b || !c

// You must not mix data types. The following would not work:
// def illegal_mixup(a: int, b: float): int = a + b

// You can give names to temporary values.
def let_example(a: int): int = let square = a * a in square + 1
def complex_let_example(a: int): int = (
	let b = 2 * a,
		c = b + 1,
		c = c + b
	in
		a + b + c
)

// There is a basic if-then-else expression for conditional evaluation.
def if_example(a: float, b: float): int = (
	if a > b then 0 else 1
)
